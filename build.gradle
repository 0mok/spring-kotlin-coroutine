buildscript {
    ext.kotlin_version = "1.1.50"
    ext.kotlin_coroutines_version = "0.18"
    ext.spock_version = '1.1-groovy-2.4'
    ext.spring_version = "4.3.11.RELEASE"
    ext.spring_boot_version = "1.5.7.RELEASE"
    ext['reactor.version'] = '3.0.7.RELEASE'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
        }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7"
        classpath "pl.allegro.tech.build:axion-release-plugin:1.6.0"
        classpath "com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "kotlin"

    compileJava {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }

    [compileJava, compileTestJava, javadoc]*.options*.encoding = "UTF-8"
    [compileKotlin,compileTestKotlin]*.kotlinOptions*.jvmTarget = "1.8"

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    }

    kotlin {
        experimental {
            coroutines "enable"
        }
    }
}

project(":spring-kotlin-coroutine") {
    apply plugin: 'pl.allegro.tech.build.axion-release'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'groovy'
    apply plugin: 'nebula.optional-base'
    apply plugin: 'maven-publish'

    group "org.springframework.kotlin"
    project.version = scmVersion.version

    compileTestGroovy.classpath += files(compileTestKotlin.destinationDir)

    dependencies {
        compile "org.springframework:spring-context-support:$spring_version"
        compile "org.springframework:spring-webmvc:$spring_version", optional
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-reactor:$kotlin_coroutines_version", optional
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-rx1:$kotlin_coroutines_version", optional
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$kotlin_coroutines_version", optional
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

        testCompile "net.sf.ehcache:ehcache:2.10.3"
        testCompile "org.spockframework:spock-core:$spock_version"
        testCompile "org.spockframework:spock-spring:$spock_version"
        testCompile "org.springframework.boot:spring-boot-autoconfigure:$spring_boot_version"
        testCompile "org.springframework.boot:spring-boot-starter-test:$spring_boot_version"
        testCompile "org.springframework.boot:spring-boot-starter-web:$spring_boot_version"
    }

    bintray {
        user = "konrad-kaminski"
        key = System.getenv('BINTRAY_KEY')
        dryRun = false
        publish = true
        publications = ['Bintray']
        pkg {
            repo = 'maven'
            name = 'spring-kotlin-coroutine'
            licenses = ['Apache-2.0']
            vcsUrl = 'git@github.com:konrad-kaminski/spring-kotlin-coroutine.git'
            websiteUrl = 'https://github.com/konrad-kaminski/spring-kotlin-coroutine'
            issueTrackerUrl = 'https://github.com/konrad-kaminski/spring-kotlin-coroutine/issues'
            labels = ['spring', 'kotlin', 'coroutine']
            version {
                name = project.version
                vcsTag = "release-${project.version}"
            }
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        from sourceSets.main.kotlin
    }

    task javadocJar (type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    publishing {
        publications {
            Bintray(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier 'sources'
                }

                artifact javadocJar {
                    classifier 'javadoc'
                }

                pom.withXml {
                    asNode().children().last() + {
                        resolveStrategy = Closure.DELEGATE_FIRST

                        name 'spring-kotlin-coroutine'
                        description 'Kotlin coroutine support for Spring.'
                        url 'https://github.com/konrad-kaminski/spring-kotlin-coroutine'
                        scm {
                            url 'https://github.com/konrad-kaminski/spring-kotlin-coroutine.git'
                            connection 'scm:git@github.com:konrad-kaminski/spring-kotlin-coroutine.git'
                            developerConnection 'git@github.com:konrad-kaminski/spring-kotlin-coroutine.git'
                            tag project.version
                        }
                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/license/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'konrad-kaminski'
                                name 'Konrad Kami≈Ñski'
                                email 'kaminski.konrad@gmail.com'
                            }
                        }
                    }
                }
            }
        }
    }
}

project(":spring-kotlin-coroutine-demo") {
    apply plugin: "org.springframework.boot"

    dependencies {
        compile project(":spring-kotlin-coroutine")

        compile "net.sf.ehcache:ehcache:2.10.3"
        compile "org.springframework.boot:spring-boot-autoconfigure:$spring_boot_version"
        compile "org.springframework.boot:spring-boot-starter:$spring_boot_version"
        compile "org.springframework.boot:spring-boot-starter-web:$spring_boot_version"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.2"
    distributionType = "ALL"
}
